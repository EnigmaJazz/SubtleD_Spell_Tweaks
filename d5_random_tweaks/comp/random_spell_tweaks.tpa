
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								SPELL TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//DEFINE_ACTION_FUNCTION personal_spell_tweaks BEGIN


// various spell tweaks_____________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_magic_missile BEGIN
// MM: do missile damage (?)
COPY_EXISTING ~spwi112.spl~ ~override~
  SAY UNIDENTIFIED_DESC @21122
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_color_spray BEGIN
// color spray can blind for 1 round regardless of target level
COPY_EXISTING ~spwi105.spl~ ~override~
//  SAY UNIDENTIFIED_DESC @21052
  LPF DELETE_EFFECT INT_VAR match_opcocde = 128 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 74 opcode = 128 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 74 target = 2 power = 1 timing = 0 duration = 7 savingthrow = 1 END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_pro_petrification BEGIN
/*
// SR pro petrification --> mirrored eyes... or pro from transmutations
COPY_EXISTING ~spwi108.spl~ ~override~
  SAY NAME1 @21081
  SAY UNIDENTIFIED_DESC @21082
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 101 match_parameter2 = 134 opcode = 83 parameter2 = 64 END
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 101 match_parameter2 = 134 opcode = 206 parameter1 = 0 STR_VAR resource = ~SPIN839~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 101 match_parameter2 = 134 opcode = 206 parameter1 = 0 STR_VAR resource = ~SPIN883~ END
IF_EXISTS BUT_ONLY
*/

// okay no - instead give evasion vs. gazes

LAF d5_resolve_state STR_VAR new_state_id = ~D5_GAZEEVADE~ RET new_state_ind END
OUTER_SET gaze_evasion_state = %new_state_ind%
<<<<<<<< d5/d5gazeev.2da
2DA V1.0 
RES
>>>>>>>> 
COPY ~d5/d5gazeev.2da~ ~override/d5gazeev.2da~ 
COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
	READ_SHORT 0x68 num_ab
	PATCH_IF (num_ab > 0) BEGIN
	  GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
	  PHP_EACH ab_array AS int => ab_off BEGIN
		READ_SHORT (ab_off + 0x26) spl_proj
		PATCH_IF (spl_proj = 64) BEGIN
		  INNER_ACTION BEGIN
			APPEND ~d5gazeev.2da~ ~%SOURCE_RES% 	SPL~ UNLESS ~%SOURCE_RES%~
		  END
		END
	  END
	END
  END
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
	READ_SHORT 0x68 num_ab
	PATCH_IF (num_ab > 0) BEGIN
	  GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
	  PHP_EACH ab_array AS int => ab_off BEGIN
		READ_SHORT (ab_off + 0x2a) itm_proj
		PATCH_IF (spl_proj = 64) BEGIN
		  INNER_ACTION BEGIN
			APPEND ~d5gazeev.2da~ ~%SOURCE_RES% 	ITM~ UNLESS ~%SOURCE_RES%~
		  END
		END
	  END
	END
  END
BUT_ONLY
COPY_EXISTING ~d5gazeev.2da~ ~override~ 
  COUNT_2DA_COLS cols
  COUNT_2DA_ROWS cols rows
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 0 cols gaze_evade_res
	READ_2DA_ENTRY row 1 cols gaze_evade_type
	PATCH_IF (~%gaze_evade_type%~ STRING_EQUAL_CASE ~spl~) BEGIN 
	  PATCH_IF (FILE_EXISTS_IN_GAME ~%hold_evade_res%.spl~) BEGIN
		INNER_ACTION BEGIN
		  LAF add_evade_spell INT_VAR evasion_spellstate = %gaze_evasion_state% evasion_save = 1 STR_VAR evade_ext = ~spl~ evade_condition = ~spellstate~ evade_res = EVAL ~%gaze_evade_res%~ evade_prefix = ~D5VG~ END
		END
	  END
	END
	PATCH_IF (~%gaze_evade_type%~ STRING_EQUAL_CASE ~itm~) BEGIN 
	  PATCH_IF (FILE_EXISTS_IN_GAME ~%hold_evade_res%.itm~) BEGIN
		INNER_ACTION BEGIN
		  LAF add_evade_spell INT_VAR evasion_spellstate = %gaze_evasion_state% evasion_save = 1 STR_VAR evade_ext = ~itm~ evade_condition = ~spellstate~ evade_res = EVAL ~%gaze_evade_res%~ evade_prefix = ~D5VG~ END
		END
	  END
	END
  END
BUT_ONLY

COPY_EXISTING ~spwi108.spl~ ~override~
  SAY NAME1 @21081
  SAY UNIDENTIFIED_DESC @21083
  LPF ALTER_EFFECT INT_VAR match_opcode = 101 match_parameter2 = 134 opcode = 328 parameter1 = 0 parameter2 = %gaze_evasion_state% END
END

//__________________________________________________________________________________

/*
DEFINE_ACTION_FUNCTION tweak_charm_person BEGIN
// charm person set APR to 0, disable spellcasting
// ...or, a chunky thac0 penalty
// ...and canceled if take damage, *or if victim damages someone else*
// do for druid spell too
COPY_EXISTING ~spwi104.spl~ ~override~
  SAY UNIDENTIFIED_DESC @21042
IF_EXISTS BUT_ONLY

END
*/

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_sunscorch BEGIN
// reduce damage, extend blindness, make undead unable to save vs. blindness

ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
 ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_SUNSCORCH[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_SUNSCORCH~ RET spell_res END
  COPY ~d5_random_tweaks/data/spell_tweaks/d5dvsuns.spl~ ~override/%spell_res%.spl~
	SAY UNIDENTIFIED_DESC @11512
 END
END

// maybe make undead unable to save against the blindness?

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_faerie_fire BEGIN
// 
ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_FAERIE_FIRE[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_FAERIE_FIRE~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 savingthrow = 0 END
	LPF DELETE_EFFECT INT_VAR match_opcode = 41 END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 0 opcode = 215 parameter1 = 0 parameter2 = 1 STR_VAR resource = ~d5_ffire~ END
  IF_EXISTS BUT_ONLY
END

COPY ~d5_random_tweaks/data/spell_tweaks/d5_ffire.vvc~ ~override~

ACTION_IF (FILE_EXISTS_IN_GAME ~dvffire.pro~) BEGIN
  COPY_EXISTING ~dvffire.pro~ ~override~
	WRITE_SHORT 0x206 160
  BUT_ONLY
END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_luck BEGIN
// Luck: AoE, 5-round duration
COPY_EXISTING ~spwi209.spl~ ~override~
  SAY UNIDENTIFIED_DESC @22092
	LPF ALTER_SPELL_HEADER INT_VAR target = 5 range = 30 projectile = 158 END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 18 duration = 30 END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_mirror_image BEGIN
// move to level 3
 ACTION_IF FILE_EXISTS_IN_GAME ~spwi212.spl~ THEN BEGIN
	COPY_EXISTING ~spwi212.spl~ ~override~
	ADD_SPELL ~override/spwi212.spl~ 2 3 WIZARD_MIRROR_IMAGE_D5
		WRITE_LONG 0x34 3
		READ_LONG 0x50 "valid"
		PATCH_IF (%valid% >= 0) BEGIN // verify name is valid
			READ_STRREF 0x50 "desc"
			INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
				REPLACE_TEXTUALLY ~Level: 2~ ~Level: 3~
			END
			SAY_EVALUATED 0x50 ~%new_desc%~
		END
	ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN
		APPEND ~hidespl.2da~ ~spwi212	1		0		0~
	END
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~WIZARD_MIRROR_IMAGE_D5~
		RET spell_res
	END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl96.itm~ THEN BEGIN
		COPY_EXISTING ~scrl96.itm~ ~override~
			WRITE_LONG 0x34 300
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 STR_VAR resource = EVAL ~%spell_res%~ END
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 STR_VAR resource = EVAL ~%spell_res%~ END					
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 148 STR_VAR resource = EVAL ~%spell_res%~ END
			READ_LONG 0x54 "valid"
			PATCH_IF (%valid% >= 0) BEGIN // verify name is valid
				READ_STRREF 0x54 "desc"
				INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
					REPLACE_TEXTUALLY ~Level: 2~ ~Level: 3~
				END
				SAY_EVALUATED 0x54 ~%new_desc%~
			END
		BUT_ONLY
	END
	ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => dv BEGIN
	  PRINT ~%cre% => %dv%~ 
	  COPY_EXISTING ~%cre%~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			READ_BYTE 0x273 class
			PATCH_IF (class == 1 || class == 13 || class == 14 || class == 7 || class == 10 || class == 17 || class == 19 || class == 5) BEGIN // wizard casters
				REMOVE_MEMORIZED_SPELL ~spwi212~
				REMOVE_KNOWN_SPELL ~spwi212~
//				ADD_KNOWN_SPELL ~%spell_res%~ #2 ~wizard~
//				ADD_MEMORIZED_SPELL ~%spell_res%~ #2 ~wizard~
			END
		END
	  BUT_ONLY
	END
 END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_stinking_cloud BEGIN
// 
COPY_EXISTING ~spwi213.spl~ ~override~
  SAY UNIDENTIFIED_DESC @22132
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 2 timing = 1 STR_VAR resource = ~spwi213~ END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 60 duration = 13 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 2 parameter2 = 3 duration = 13 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 142 duration = 24 END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 60 opcode = 126 parameter1 = 50 parameter2 = 2 /*duration = 24*/ savingthrow = 0 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 opcode = 278 target = 2 parameter1 = (0 - 2) parameter2 = 0 resist_dispel = 1 timing = 0 duration = 24 savingthrow = 4 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 opcode = 0 target = 2 parameter1 = (0 - 2) parameter2 = 0 resist_dispel = 1 timing = 0 duration = 24 savingthrow = 4 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 99 opcode = 189 target = 2 parameter1 = (0 - 2) parameter2 = 0 resist_dispel = 1 timing = 0 duration = 24 savingthrow = 4 END
  
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_web BEGIN
// web = Slow + movement reduction (+ APR reduction?)
COPY_EXISTING  ~spwi215.spl~ ~override~
				~spwi215d.spl~ ~override~
  SAY UNIDENTIFIED_DESC @22152
  LPF DELETE_EFFECT INT_VAR match_opcode = 126 END
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 109 opcode = 126 target = 2 parameter1 = 30 parameter2 = 2 savingthrow = 0 END
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 109 opcode = 40 target = 2 END
  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 109 opcode = 1 target = 2 parameter1 = 0 parameter2 = 3 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 109 END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_glitterdust BEGIN
// 
ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
  COPY_EXISTING ~spwi224.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 savingthrow = 0 END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 savingthrow = 0 END
  IF_EXISTS BUT_ONLY
END

ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
  COPY_EXISTING ~spwi224.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 savingthrow = 0 END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 54 opcode = 0 savingthrow = 0 END
  IF_EXISTS BUT_ONLY
END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_decastave BEGIN
// decastave: deafness on hit
COPY_EXISTING ~decasta.itm~ ~override~
			  ~cdideca.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 248 target = 1 timing = 2 STR_VAR resource = ~d5decas~ END
IF_EXISTS BUT_ONLY

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]WIZARD_DECASTAVE[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~WIZARD_DECASTAVE~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	SAY UNIDENTIFIED_DESC @22512
  IF_EXISTS BUT_ONLY
  CREATE EFF ~d5decas~
	WRITE_LONG 0x10 80
	WRITE_LONG 0x14 2
	WRITE_LONG 0x18 1
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 18
	WRITE_SHORT 0x2c 100
	WRITE_BYTE 0x40 (THIS BOR 0b00000001)
END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_slow_poison BEGIN
// 
COPY_EXISTING ~sppr212.spl~ ~override~
  SAY UNIDENTIFIED_DESC @12122

IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_barkskin BEGIN
// 
CREATE EFF ~d5bbark~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 2
  WRITE_LONG 0x18 0
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
//  WRITE_LONG 0x24 0
  WRITE_LONG 0x28 120
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5bbark~ #8
  WRITE_LONG 0x48 102

COPY_EXISTING  ~sppr202.spl~ ~override~
				~staf14a.spl~ ~override~
				~d5bp202.spl~ ~override~
				~qdfv_202.spl~ ~override~
				~d5rnm21.spl~ ~override~
				~d5rt202.spl~ ~override~
  SAY UNIDENTIFIED_DESC @12022
// no stacking
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 321 STR_VAR match_resource = ~sppr202~ resource = ~staf14a~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 321 STR_VAR match_resource = ~sppr202~ resource = ~d5bp202~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 321 STR_VAR match_resource = ~sppr202~ resource = ~qdfv_202~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 321 STR_VAR match_resource = ~sppr202~ resource = ~d5rnm21~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 321 STR_VAR match_resource = ~sppr202~ resource = ~d5rt202~ END
// remove AC bonus, add stoneskin effect
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 0 opcode = 177 parameter1 = 0 parameter2 = 2 timing = 0 duration = 120 STR_VAR resource = ~d5bbark~ END
IF_EXISTS BUT_ONLY

COPY_EXISTING  ~sppr202.spl~ ~override~
				~staf14a.spl~ ~override~
				~d5bp202.spl~ ~override~
				~qdfv_202.spl~ ~override~
				~d5rnm21.spl~ ~override~
				~d5rt202.spl~ ~override~
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_timing = 0 target = 1 duration = 120 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_timing = 4 target = 1 duration = 120 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_timing = 1 target = 1 END
IF_EXISTS BUT_ONLY

COPY ~d5_random_tweaks/data/item_tweaks/d5leat14.spl~ ~override/d5bbark.spl~
//  LPF ALTER_EFFECT INT_VAR match_opcode = 321 STR_VAR resource = ~d5bbark~ END

COPY_EXISTING ~tomegol4.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
	REPLACE_TEXTUALLY ~!CheckStatGT(LastSummonerOf(Myself),0,STONESKINSGOLEM)~ ~~
	REPLACE_TEXTUALLY ~!CheckStatGT(Player[1-6],0,STONESKINSGOLEM)~ ~~
  COMPILE_BAF_TO_BCS
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_find_traps BEGIN
// find traps: last a long time, (fire every 3 seconds (ok not the 2nd part))
COPY_EXISTING ~sppr205.spl~ ~override~
  SAY UNIDENTIFIED_DESC @12052
  LPF DELETE_EFFECT INT_VAR match_opcode = 146 STR_VAR match_resource = ~sppr205d~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 0 duration = 600 STR_VAR resource = ~d5findt~ END
IF_EXISTS BUT_ONLY

CREATE EFF ~d5findt~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x18 1
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
  WRITE_LONG 0x24 0
  WRITE_LONG 0x28 600
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~sppr205d~ #8
  WRITE_LONG 0x28 102

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_good_berry BEGIN
//
ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_GOOD_BERRIES[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_GOOD_BERRIES~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	READ_LONG 0x34 res_level

  OUTER_SPRINT nuberry_desc @12072
  OUTER_INNER_PATCH_SAVE level_berry_desc ~%nuberry_desc%~ BEGIN
	REPLACE_TEXTUALLY ~Level: 2~ ~Level: %res_level%~
  END

  COPY ~d5_random_tweaks/data/spell_tweaks/d5goodb.spl~ ~override/%spell_res%.spl~
	SAY NAME1 @12071
	SAY UNIDENTIFIED_DESC ~%level_berry_desc%~
	LPF ALTER_EFFECT INT_VAR power = 0 END
	WRITE_LONG 0x34 res_level
END

COPY ~d5_random_tweaks/data/spell_tweaks/d5goodb.spl~ ~override/sppr207.spl~
  SAY NAME1 @12071
  SAY UNIDENTIFIED_DESC @12072
  LPF ALTER_EFFECT INT_VAR power = 0 END
  WRITE_LONG 0x34 2

ACTION_IF (GAME_IS ~iwdee~) BEGIN
  COPY_EXISTING ~sppr207.spl~ ~override/sppr215.spl~
END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_alicorn_lance BEGIN
// 

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_ALICORN_LANCE[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_ALICORN_LANCE~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	SAY UNIDENTIFIED_DESC @12512
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 12 parameter2 = 4194304 dicenumber = 4 savingthrow = 0 END
END

// maybe make undead unable to save against the blindness?

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_haste_slow BEGIN

COPY_EXISTING ~msectype.2da~ ~override~
  READ_2DA_ENTRIES_NOW rows 2
  FOR (row = 1; row < rows; ++row) BEGIN 
	READ_2DA_ENTRY_FORMER rows row 0 ~sectype~ 
	PATCH_IF (~%sectype%~ STRING_EQUAL_CASE ~k1#Haste~) BEGIN
	  SET k1haste_row = (%row% - 1)
	END
	PATCH_IF (~%sectype%~ STRING_EQUAL_CASE ~k1#Slow~) BEGIN
	  SET k1slow_row = (%row% - 1)
	END
  END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
	READ_BYTE 0x27 sec_type
	PATCH_IF (sec_type = %k1haste_row%) BEGIN
	  SPRINT $haste_list(~%SOURCE_RES%~) ~haste~
	  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 176 opcode = 226 parameter1 = 1 parameter2 = %k1slow_row% STR_VAR resource = ~d5xw305~ insert = ~first~ END
	END
	PATCH_IF (sec_type = %k1slow_row%) BEGIN
	  SPRINT $slow_list(~%SOURCE_RES%~) ~slow~
	  LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 40 opcode = 226 parameter1 = 1 parameter2 = %k1haste_row% STR_VAR resource = ~d5xw312~ insert = ~first~ END
	END
  END
BUT_ONLY

COPY_EXISTING ~spwi312.spl~ ~override/d5xw305.spl~
  WRITE_BYTE 0x27 0
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 projectile = 1 END
  LPF DELETE_EFFECT END
  PHP_EACH haste_list AS haste_res => type BEGIN
	PATCH_IF (~%type%~ STRING_EQUAL_CASE ~haste~) BEGIN
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 STR_VAR resource = EVAL ~%haste_res%~ END
	END
  END

COPY_EXISTING ~spwi312.spl~ ~override/d5xw312.spl~
  WRITE_BYTE 0x27 0
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 projectile = 1 END
  LPF DELETE_EFFECT END
  PHP_EACH slow_list AS slow_res => type BEGIN
	PATCH_IF (~%type%~ STRING_EQUAL_CASE ~slow~) BEGIN
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 1 STR_VAR resource = EVAL ~%slow_res%~ END
	END
  END

COPY_EXISTING ~spturni2.bam~ ~override/d5turni2.bam~
IF_EXISTS BUT_ONLY

COPY ~d5_random_tweaks/data/spell_tweaks/d5turni2.vvc~ ~override~
COPY ~d5_random_tweaks/data/spell_tweaks/d5noanim.bam~ ~override/spturni2.bam~

COPY_EXISTING ~k1#scre.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 STR_VAR resource = ~d5turni2~ END   
IF_EXISTS BUT_ONLY
COPY_EXISTING ~spwi519.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 215 STR_VAR resource = ~d5turni2~ END   
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

/*
DEFINE_ACTION_FUNCTION tweak_dire_charm BEGIN
// dire charm disable spellcasting
COPY_EXISTING ~spwi316.spl~ ~override~
  SAY UNIDENTIFIED_DESC @23162
IF_EXISTS BUT_ONLY

END
*/

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_hold_control_undead BEGIN

COPY_EXISTING ~spwi324.spl~ ~override~
			  ~spwi324d.spl~ ~override~
			  ~spwi720.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR resist_dispel = 3 END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_exaltation_spiritual_clarity BEGIN
// 
ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_EXALTATION[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_EXALTATION~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	SAY UNIDENTIFIED_DESC @13232
	LPF ALTER_SPELL_HEADER INT_VAR speed = 4 END
  IF_EXISTS BUT_ONLY
  COPY_EXISTING ~%spell_res%.spl~ ~override/sppr350.spl~
	SAY NAME1 @13501
	SAY UNIDENTIFIED_DESC @13502
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 3
	LPF ALTER_SPELL_HEADER INT_VAR speed = 1 END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 60 duration = 30 END
  IF_EXISTS BUT_ONLY
END

COPY_EXISTING ~ohtyr1.spl~ ~override~
  SAY UNIDENTIFIED_DESC @13232
  LPF ALTER_SPELL_HEADER INT_VAR speed = 4 END
IF_EXISTS BUT_ONLY

ACTION_IF (FILE_EXISTS_IN_GAME ~ohtyr1.spl~) AND !(FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_EXALTATION[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  COPY_EXISTING ~ohtyr1.spl~ ~override/sppr350.spl~
	SAY NAME1 @13501
	SAY UNIDENTIFIED_DESC @13502
	WRITE_SHORT 0x1c 2
	WRITE_LONG 0x34 3
	LPF ALTER_SPELL_HEADER INT_VAR speed = 1 END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 60 duration = 30 END
  BUT_ONLY
END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_moonblade BEGIN
// moonblade: slow undead on hit
COPY_EXISTING ~moonbla.itm~ ~override~
			 ~cdimoonb.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR thac0_bonus = 4 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 60 END
//  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 248 target = 1 timing = 2 STR_VAR resource = ~d5moonb~ END
  LPF ADD_ITEM_EFFECT INT_VAR header_type = 1 opcode = 146 target = 2 parameter2 = 1 timing = 1 probability1 = 50 STR_VAR resource = ~d5moonb1~ END
  LPF ADD_ITEM_EFFECT INT_VAR header_type = 1 opcode = 177 target = 2 parameter1 = 4 parameter2 = 3 timing = 0 duration = 18 STR_VAR resource = ~d5moonb2~ END
IF_EXISTS BUT_ONLY

/*
CREATE EFF ~d5moonb~
  WRITE_LONG 0x10 177
  WRITE_LONG 0x14 2
  WRITE_LONG 0x18 1
  WRITE_LONG 0x1c 4
  WRITE_LONG 0x20 3
  WRITE_LONG 0x24 0
  WRITE_LONG 0x28 18
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5moonb2~ #8
*/

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_MOONBLADE[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_MOONBLADE~ RET spell_res END
  COPY_EXISTING ~%spell_res%.spl~ ~override~
	SAY UNIDENTIFIED_DESC @13512
  IF_EXISTS BUT_ONLY
  CREATE EFF ~d5moonb2~
	WRITE_LONG 0x10 40
	WRITE_LONG 0x14 2
	WRITE_LONG 0x18 1
	WRITE_LONG 0x24 0
	WRITE_LONG 0x28 18
	WRITE_SHORT 0x2c 100
	WRITE_BYTE 0x40 (THIS BOR 0b00000001)
	WRITE_LONG 0x44 (0 - 2)
END

COPY ~d5_random_tweaks/data/spell_tweaks/d5moonb1.spl~ ~override~

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_otilukes_sphere BEGIN
// otilukes: allow inventory access
// (remove Hold2 effect, disable all buttons)
COPY_EXISTING ~spwi413a.spl~ ~override~
			  ~spwi413d.spl~ ~override~
//  SAY UNIDENTIFIED_DESC @24132
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 145 parameter1 = 0 parameter2 = 0 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 145 parameter1 = 0 parameter2 = 1 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 145 parameter1 = 0 parameter2 = 2 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 0 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 1 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 2 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 3 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 4 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 5 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 6 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 7 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 8 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 9 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 10 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 11 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 12 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 13 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 185 opcode = 144 parameter1 = 0 parameter2 = 14 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 185 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 318 target = 2 parameter2 = 49 timing = 0 duration = 0 STR_VAR resource = ~%SOURCE_RES%~ END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 176 opcode = 185 parameter1 = 0 parameter2 = 2 END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_neutralize_poison BEGIN
// 
COPY_EXISTING ~sppr404.spl~ ~override~
  SAY NAME1 @14041
  PATCH_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
	SAY UNIDENTIFIED_DESC @14042
  END
  PATCH_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
	SAY UNIDENTIFIED_DESC @14043
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 162 target = 2 power = 4 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 163 target = 2 power = 4 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 240 target = 2 power = 4 parameter2 = 13 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 79 target = 2 power = 4 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 75 target = 2 power = 4 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 81 target = 2 power = 4 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 240 target = 2 power = 4 parameter2 = 7 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 240 target = 2 power = 4 parameter2 = 8 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 240 target = 2 power = 4 parameter2 = 112 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 240 target = 2 power = 4 parameter2 = 101 timing = 1 resist_dispel = 3 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 93 target = 2 power = 4 parameter1 = 1 parameter2 = 1 timing = 1 resist_dispel = 2 END
  PATCH_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
	LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 101 match_parameter2 = 25 parameter2 = 78 END
	LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 169 match_parameter2 = 6 parameter2 = 7 END
  END

IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

/*
DEFINE_ACTION_FUNCTION tweak_death_ward BEGIN
// 
COPY_EXISTING ~~ ~override~
  SAY UNIDENTIFIED_DESC @14092
IF_EXISTS BUT_ONLY

END
*/

//__________________________________________________________________________________

/*
DEFINE_ACTION_FUNCTION tweak_negative_plane_protection BEGIN
// NPP reduce damage from Larloch's drain, skull trap, ADHW...?
// 
COPY_EXISTING ~~ ~override~
  SAY UNIDENTIFIED_DESC @14132
IF_EXISTS BUT_ONLY

END
*/

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_waves_of_fatigue BEGIN
// 
COPY_EXISTING ~msectype.2da~ ~override~
	READ_2DA_ENTRIES_NOW rows 2
	FOR (row = 1; row < rows; ++row) BEGIN 
	  READ_2DA_ENTRY_FORMER rows row 0 ~sectype~ 
	  PATCH_IF (~%sectype%~ STRING_EQUAL_CASE ~k1#Haste~) BEGIN
	    SET k1haste_row = %row%
	  END
	END
BUT_ONLY
COPY_EXISTING ~spwi508d.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 22 target = 2 power = 5 parameter1 = (0 - 2) parameter2 = 0 timing = 0 duration = 30 resist_dispel = 1 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 221 target = 2 power = 5 parameter1 = 9 parameter2 = %k1haste_row% timing = 0 duration = 30 resist_dispel = 1 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 325 target = 2 power = 5 parameter1 = (0 - 2) parameter2 = 0 timing = 0 duration = 30 resist_dispel = 1 savingthrow = 16 savebonus = (0 - 2) END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 189 target = 2 power = 5 parameter1 = (0 - 2) parameter2 = 0 timing = 0 duration = 30 resist_dispel = 1 savingthrow = 16 savebonus = (0 - 2) END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 39 target = 2 power = 5 parameter1 = 0 parameter2 = 1 timing = 0 duration = 6 resist_dispel = 1 savingthrow = 16 savebonus = (0 - 2) END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 1 target = 2 power = 5 parameter1 = (0 - 6) parameter2 = 0 timing = 0 duration = 30 resist_dispel = 1 savingthrow = 16 savebonus = (0 - 2) END
  LPF DELETE_EFFECT INT_VAR match_opcode = 93 END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 2 power = 5 timing = 1 resist_dispel = 1 savingthrow = 16 savebonus = (0 - 2) STR_VAR resource = ~spwi508d~ END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_phantom_blade BEGIN
// phantom blade: do 1d8 stunning +1d8 magic damage; casting failure on hit if SR
COPY_EXISTING ~phanblad.itm~ ~override~
  LPF DELETE_EFFECT INT_VAR match_opcode = 177 STR_VAR match_resource = ~phanblad~ END
//  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 248 target = 1 timing = 2 STR_VAR resource = ~d5phanb~ END
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 1 dicenumber = 1 dicesize = 8 damage_type = 5 END
  LPF ADD_ITEM_EFFECT INT_VAR header_type = 1 opcode = 146 target = 2 parameter2 = 1 timing = 1 probability1 = 50 STR_VAR resource = ~d5phanb1~ END
  LPF ALTER_EFFECT INT_VAR silent = 1 header_type = 1 match_opcode = 12 match_parameter2 = 64 dicesize = 8 END
IF_EXISTS BUT_ONLY

/*
CREATE EFF ~d5phanb~

COPY_EXISTING ~spwi518.spl~ ~override~
  SAY UNIDENTIFIED_DESC @25182
  WRITE_BYTE 0x25 5
IF_EXISTS BUT_ONLY

END
*/

COPY_EXISTING ~sppr311.spl~ ~override/d5phanb1.spl~
  WRITE_SHORT 0x1c 4
  WRITE_BYTE 0x25 0
  WRITE_LONG 0x34 5
  LPF ALTER_EFFECT INT_VAR silent = 1 match_duration = 60 duration = 18 END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_sunfire BEGIN
// sunfire: change to Missile Storm - cast MM at everybody
COPY_EXISTING ~spwi523.spl~ ~override~
  SAY NAME1 @25231
  LPF DELETE_EFFECT END
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 projectile = 159 END
  PATCH_IF !(MOD_IS_INSTALLED ~tomeandblood.tp2~ ~62~) BEGIN
	SAY UNIDENTIFIED_DESC @25232
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~SPWI112~ END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 4 duration = 1 STR_VAR resource = ~SPWI112~ END
  END
  PATCH_IF (MOD_IS_INSTALLED ~tomeandblood.tp2~ ~62~) BEGIN
	SAY UNIDENTIFIED_DESC @25233
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~SPWI217~ END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 4 duration = 1 STR_VAR resource = ~SPWI217~ END
  END
IF_EXISTS BUT_ONLY
	
END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_wondrous_recall BEGIN
// wondrous recall: restore all 1st & 2nd-level slots
COPY_EXISTING ~sppr611.spl~ ~override~
  SAY UNIDENTIFIED_DESC @16112
  LPF DELETE_EFFECT INT_VAR match_opcode = 261 END
  FOR (spls = 1; spls < 15; ++spls) BEGIN
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 261 target = 1 parameter1 = 2 parameter2 = 1 timing = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 261 target = 1 parameter1 = 1 parameter2 = 1 timing = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5shm-1~ END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5shm-2~ END
  END
IF_EXISTS BUT_ONLY

ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~potn43.spl~) BEGIN
	COPY_EXISTING ~sppr611.spl~ ~override/potn43.spl~
	  FOR (spls = 1; spls < 15; ++spls) BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 261 target = 1 parameter1 = 2 parameter2 = 0 timing = 1 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 261 target = 1 parameter1 = 1 parameter2 = 0 timing = 1 END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5shm-1~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5shm-2~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-1~ END
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-2~ END
	  END
	IF_EXISTS BUT_ONLY
  END
  COPY_EXISTING ~potn43.itm~ ~override~
	SAY IDENTIFIED_DESC @16113
  IF_EXISTS BUT_ONLY
END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_physical_mirror BEGIN
// 
COPY_EXISTING ~sppr613.spl~ ~override~
//  WRITE_BYTE 0x27 0	//	remove combat protections sectype
  PATCH_FOR_EACH dur IN ~6~ ~12~ ~18~ ~24~ ~30~ ~36~ ~42~ ~48~ ~54~ ~60~ ~66~ ~72~ ~78~ ~84~ ~90~ ~96~ ~102~ ~108~ ~114~ BEGIN
	SET new_dur = (dur - 3)
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 146 duration = %dur% duration = %new_dur% STR_VAR match_resource = ~sppr613d~ END
  END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 146 duration = 114 duration = 117 STR_VAR match_resource = ~sppr613d~ insert = ~below~ END
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 142 opcode = 22 parameter1 = 2 parameter2 = 0 END
IF_EXISTS BUT_ONLY
COPY_EXISTING ~sppr613d.spl~ ~override~
//  WRITE_BYTE 0x27 0	//	remove combat protections sectype
  PATCH_IF (GAME_IS ~bgee bg2ee iwdee eet~) BEGIN
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 159 duration = 7 END
	LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~sppr613d~ END
  END
  PATCH_IF (ENGINE_IS ~tob bgt~) BEGIN
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 159 duration = 3 END
  END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_sols_orb BEGIN
// 
COPY_EXISTING ~bulletex.pro~ ~override~
  WRITE_SHORT 0x204 192
  WRITE_SHORT 0x206 192
IF_EXISTS BUT_ONLY

COPY_EXISTING ~sorb.itm~ ~override~
  LPF ALTER_ITEM_HEADER INT_VAR range = 30 speed = 0 END
  LPF ADD_ITEM_EQEFFECT INT_VAR target = 1 opcode = 190 parameter1 = 5 timing = 2 END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_mantle BEGIN
// Mantle/SR prismatic mantle --> "Iron Skin" 
// ...Iron Skin?  stoneskin + mantle?  use insanctibility trick to remove mantle
// ...opcode 218 sets stat 88

APPEND ~splprot.2da~ ~D5_STONESKIN%TAB%88%TAB%1%TAB%4~

COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_STONESKIN~ BEGIN
	    SET stoneskin_row = %row%
	  END
	END
BUT_ONLY

ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
  COPY_EXISTING ~spwi408.spl~ ~override~
	READ_LONG 0x08 ss_name
	READ_LONG 0x50 ss_desc
  COPY_EXISTING ~sppr506.spl~ ~override~
	WRITE_LONG 0x08 ss_name
	WRITE_LONG 0x50 ss_desc
	WRITE_ASCII 0x3a ~spwi408c~ #8
	LPF ALTER_SPELL_HEADER STR_VAR icon = ~spwi408b~ END
END

// ***** double the number of skins, and 206 vs. spwi408

COPY_EXISTING ~spwi408.spl~ ~override/spwi708.spl~
  SAY NAME1 @27081
  SAY UNIDENTIFIED_DESC @27082
  WRITE_LONG 0x34 7
  WRITE_ASCII 0x3a ~sppr506c~ #8
  LPF ALTER_SPELL_HEADER STR_VAR icon = ~sppr506b~ END
  LPF ALTER_EFFECT INT_VAR silent = 1 power = 7 END
  PATCH_FOR_EACH missile IN ~1~ ~3~ ~4~ ~101~ ~102~ ~6~ ~9~ ~11~ ~14~ ~15~ ~16~ ~19~ ~26~ ~29~ ~31~ ~34~ ~257~ BEGIN
	LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 83 parameter1 = 0 parameter2 = %missile% END
  END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 120 parameter1 = 0 parameter2 = 2 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 328 parameter1 = 0 parameter2 = 2 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 328 parameter2 = 121 special = 1 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 233 parameter1 = 1 parameter2 = 128 END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 206 parameter1 = 0 STR_VAR resource = ~spwi408~ END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 206 parameter1 = 0 STR_VAR resource = ~sppr506~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 1 power = 7 parameter2 = 0 STR_VAR resource = ~spprotec~ END
  LPF CLONE_EFFECT INT_VAR multi_match = 1 silent = 1 match_opcode = 218 opcode = 177 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~d5mantl~ END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 10 parameter1 = 15 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 9 parameter1 = 14 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 8 parameter1 = 13 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 7 parameter1 = 12 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 6 parameter1 = 11 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 5 parameter1 = 10 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 4 parameter1 = 9 END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 218 match_parameter1 = 3 parameter1 = 8 END
  LPF ALTER_EFFECT INT_VAR silent = 1 power = 7 END
IF_EXISTS BUT_ONLY

CREATE EFF ~d5mantl~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x18 1
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
  WRITE_LONG 0x24 0
  WRITE_LONG 0x28 3600
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5mantle~ #8
  WRITE_LONG 0x48 102

COPY ~d5_random_tweaks/misc/d5_base.spl~ ~override/d5mantle.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 power = 0 parameter1 = 0 timing = 1 STR_VAR resource = ~spwi708~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 power = 0 parameter2 = %stoneskin_row% timing = 1 STR_VAR resource = ~d5mantle~ END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (%SOURCE_SIZE% > 0x71) BEGIN
	LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 header_type = 1 opcode = 318 target = 2 parameter1 = 121 parameter2 = 110 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = ~first~ END
//	LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 header_type = 2 opcode = 318 target = 2 parameter1 = 121 parameter2 = 110 timing = 0 duration = 0 STR_VAR resource = EVAL ~%SOURCE_RES%~ insert = ~first~ END
  END
IF_EXISTS BUT_ONLY

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_holy_word BEGIN
// add 5-round... Slow?
// 
COPY_EXISTING ~sppr710.spl~ ~override~
  LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 80 opcode = 40 duration = 30 END
IF_EXISTS BUT_ONLY

END

//***** if SR is installed, need to do this via a pre-existing SR Slow subspell.

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_symbol_fear BEGIN
// Symbol Fear: level 7
 ACTION_IF FILE_EXISTS_IN_GAME ~spwi811.spl~ THEN BEGIN
	COPY_EXISTING ~spwi811.spl~ ~override~
	ADD_SPELL ~override/spwi811.spl~ 2 7 WIZARD_SYMBOL_FEAR_D5
		WRITE_LONG 0x34 7
		READ_LONG 0x50 "valid"
		PATCH_IF (%valid% >= 0) BEGIN // verify name is valid
			READ_STRREF 0x50 "desc"
			INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
				REPLACE_TEXTUALLY ~Level: 8~ ~Level: 7~
			END
			SAY_EVALUATED 0x50 ~%new_desc%~
		END
	ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN
		APPEND ~hidespl.2da~ ~spwi811	1		0		0~
	END
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~WIZARD_SYMBOL_FEAR_D5~
		RET spell_res
	END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl9f.itm~ THEN BEGIN
		COPY_EXISTING ~scrl9f.itm~ ~override~
			WRITE_LONG 0x34 3000
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 STR_VAR resource = EVAL ~%spell_res%~ END
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 STR_VAR resource = EVAL ~%spell_res%~ END					
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 148 STR_VAR resource = EVAL ~%spell_res%~ END
			READ_LONG 0x54 "valid"
			PATCH_IF (%valid% >= 0) BEGIN // verify name is valid
				READ_STRREF 0x54 "desc"
				INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
					REPLACE_TEXTUALLY ~Level: 8~ ~Level: 7~
				END
				SAY_EVALUATED 0x54 ~%new_desc%~
			END
		BUT_ONLY
	END
	ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => dv BEGIN
	  PRINT ~%cre% => %dv%~ 
	  COPY_EXISTING ~%cre%~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			READ_BYTE 0x273 class
			PATCH_IF (class == 1 || class == 13 || class == 14 || class == 7 || class == 10 || class == 17 || class == 19 || class == 5) BEGIN // wizard casters
				REMOVE_MEMORIZED_SPELL ~spwi811~
				REMOVE_KNOWN_SPELL ~spwi811~
//				ADD_KNOWN_SPELL ~%spell_res%~ #6 ~wizard~
//				ADD_MEMORIZED_SPELL ~%spell_res%~ #6 ~wizard~
			END
		END
	  BUT_ONLY
	END
 END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_shapechange BEGIN
// Shapechange: level 8
 ACTION_IF FILE_EXISTS_IN_GAME ~spwi916.spl~ THEN BEGIN
	COPY_EXISTING ~spwi916.spl~ ~override~
	ADD_SPELL ~override/spwi916.spl~ 2 8 WIZARD_SHAPECHANGE_D5
		WRITE_LONG 0x34 8
		READ_LONG 0x50 "valid"
		PATCH_IF (%valid% >= 0) BEGIN // verify name is valid
			READ_STRREF 0x50 "desc"
			INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
				REPLACE_TEXTUALLY ~Level: 9~ ~Level: 8~
			END
			SAY_EVALUATED 0x50 ~%new_desc%~
		END
	ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN
		APPEND ~hidespl.2da~ ~spwi916	1		0		0~
	END
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~WIZARD_SHAPECHANGE_D5~
		RET spell_res
	END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl9y.itm~ THEN BEGIN
		COPY_EXISTING ~scrl9y.itm~ ~override~
			WRITE_LONG 0x34 7500
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 STR_VAR resource = EVAL ~%spell_res%~ END
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 STR_VAR resource = EVAL ~%spell_res%~ END					
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 148 STR_VAR resource = EVAL ~%spell_res%~ END
			READ_LONG 0x54 "valid"
			PATCH_IF (%valid% >= 0) BEGIN // verify name is valid
				READ_STRREF 0x54 "desc"
				INNER_PATCH_SAVE new_desc ~%desc%~ BEGIN
					REPLACE_TEXTUALLY ~Level: 9~ ~Level: 8~
				END
				SAY_EVALUATED 0x54 ~%new_desc%~
			END
		BUT_ONLY
	END
	ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => dv BEGIN
	  PRINT ~%cre% => %dv%~ 
	  COPY_EXISTING ~%cre%~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			READ_BYTE 0x273 class
			PATCH_IF (class == 1 || class == 13 || class == 14 || class == 7 || class == 10 || class == 17 || class == 19 || class == 5) BEGIN // wizard casters
				REMOVE_MEMORIZED_SPELL ~spwi916~
				REMOVE_KNOWN_SPELL ~spwi916~
//				ADD_KNOWN_SPELL ~%spell_res%~ #7 ~wizard~
//				ADD_MEMORIZED_SPELL ~%spell_res%~ #7 ~wizard~
			END
		END
	  BUT_ONLY
	END
 END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_black_blade BEGIN
// Black Blade of Disaster: level 8
 ACTION_IF FILE_EXISTS_IN_GAME ~spwi915.spl~ THEN BEGIN
	COPY_EXISTING ~spwi915.spl~ ~override~
	ADD_SPELL ~override/spwi915.spl~ 2 8 WIZARD_BLACK_BLADE_OF_DISASTER_D5
		WRITE_LONG 0x34 8
		SAY UNIDENTIFIED_DESC @29152
	ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN
		APPEND ~hidespl.2da~ ~spwi915	1		0		0~
	END
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~WIZARD_BLACK_BLADE_OF_DISASTER_D5~
		RET spell_res
	END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl9x.itm~ THEN BEGIN
		COPY_EXISTING ~scrl9x.itm~ ~override~
			WRITE_LONG 0x34 5000
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 STR_VAR resource = EVAL ~%spell_res%~ END
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 STR_VAR resource = EVAL ~%spell_res%~ END					
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 148 STR_VAR resource = EVAL ~%spell_res%~ END
			SAY IDENTIFIED_DESC @29152
		BUT_ONLY
	END
	ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => dv BEGIN
	  PRINT ~%cre% => %dv%~ 
	  COPY_EXISTING ~%cre%~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
			READ_BYTE 0x273 class
			PATCH_IF (class == 1 || class == 13 || class == 14 || class == 7 || class == 10 || class == 17 || class == 19 || class == 5) BEGIN // wizard casters
				REMOVE_MEMORIZED_SPELL ~spwi915~
				REMOVE_KNOWN_SPELL ~spwi915~
//				ADD_KNOWN_SPELL ~%spell_res%~ #7 ~wizard~
//				ADD_MEMORIZED_SPELL ~%spell_res%~ #7 ~wizard~
			END
		END
	  BUT_ONLY
	END
 END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_energy_drain BEGIN
// Energy Drain: level 8
 ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~spwi914.spl~ THEN BEGIN
		COPY ~d5_random_tweaks/data/spell_tweaks/spwi914.spl~ ~override~
			SAY UNIDENTIFIED_DESC @29142
		ACTION_IF FILE_EXISTS_IN_GAME ~scrl9w.itm~ THEN BEGIN
			COPY_EXISTING ~scrl9w.itm~ ~override~
				SAY IDENTIFIED_DESC @29142
		END
	END
 END
 ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~spwi914.spl~ THEN BEGIN
		COPY ~d5_random_tweaks/data/spell_tweaks/spwi914sr.spl~ ~override/spwi914.spl~
		ADD_SPELL ~override/spwi914.spl~ 2 8 WIZARD_ENERGY_DRAIN_D5
			WRITE_LONG 0x34 8
			SAY UNIDENTIFIED_DESC @29143
		ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN
			APPEND ~hidespl.2da~ ~spwi914	1		0		0~
		END
		LAF RES_NUM_OF_SPELL_NAME
			STR_VAR spell_name = ~WIZARD_ENERGY_DRAIN_D5~
			RET spell_res
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~scrl9w.itm~ THEN BEGIN
			COPY_EXISTING ~scrl9w.itm~ ~override~
				WRITE_LONG 0x34 5000
				LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 146 STR_VAR resource = EVAL ~%spell_res%~ END
				LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 147 STR_VAR resource = EVAL ~%spell_res%~ END					
				LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 148 STR_VAR resource = EVAL ~%spell_res%~ END
				SAY IDENTIFIED_DESC @29143
			BUT_ONLY
		END
		ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => dv BEGIN
		  PRINT ~%cre% => %dv%~ 
		  COPY_EXISTING ~%cre%~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
				READ_BYTE 0x273 class
				PATCH_IF (class == 1 || class == 13 || class == 14 || class == 7 || class == 10 || class == 17 || class == 19 || class == 5) BEGIN // wizard casters
					REMOVE_MEMORIZED_SPELL ~spwi914~
					REMOVE_KNOWN_SPELL ~spwi914~
//					ADD_KNOWN_SPELL ~%spell_res%~ #7 ~wizard~
//					ADD_MEMORIZED_SPELL ~%spell_res%~ #7 ~wizard~
				END
			END
		  BUT_ONLY
		END
	END
 END

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_planetars BEGIN
// change to noble genies; allow the choice of either one
// 
COPY_EXISTING ~sw1h67.itm~ ~override/TG#plang.itm~	//	usuno's blade, elec. damage
  WRITE_BYTE 0X18 (THIS BOR 0B00000100)
COPY_EXISTING ~sw1h67.itm~ ~override/planetar.itm~
  WRITE_BYTE 0X18 (THIS BOR 0B00000100)
  

COPY_EXISTING ~plangood.cre~ ~override~ 	//	djinn
  SAY 0x08 @29233
  SAY 0x0c @29233
  WRITE_LONG 0x28 32517
  WRITE_SHORT 0x4c 10
  WRITE_BYTE 0x59 25
  WRITE_BYTE 0x5a 50
  WRITE_BYTE 0x5b 100
  WRITE_BYTE 0x5c 50
  WRITE_BYTE 0x5d 30
  WRITE_BYTE 0x241 0

IF_EXISTS BUT_ONLY

ACTION_IF !(FILE_EXISTS_IN_GAME ~TG#devae.itm~) AND (FILE_EXISTS_IN_GAME ~sw1h53.itm~) BEGIN
  COPY_EXISTING ~sw1h53.itm~ ~override/d5sw1h53.itm~	//	sword of flame
END
COPY_EXISTING ~planevil.cre~ ~override~ 	//	efreet
  SAY 0x08 @29243
  SAY 0x0c @29243
  WRITE_LONG 0x28 32577
  WRITE_BYTE 0x27b 35
  WRITE_BYTE 0x59 100
  WRITE_BYTE 0x5a 0
  WRITE_BYTE 0x5b 50
  WRITE_BYTE 0x5c 50
  WRITE_BYTE 0x5d 30
  REMOVE_CRE_ITEM ~TG#plang~
  REMOVE_CRE_ITEM ~planetar~
  PATCH_IF (FILE_EXISTS_IN_GAME ~TG#devae.itm~) BEGIN
	ADD_CRE_ITEM ~TG#devae~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  END
  PATCH_IF (FILE_EXISTS_IN_GAME ~d5sw1h53.itm~) BEGIN
	ADD_CRE_ITEM ~d5sw1h53~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
  END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~spwi923.spl~ ~override~
  SAY NAME1 @29231
  SAY UNIDENTIFIED_DESC @29232
IF_EXISTS BUT_ONLY

COPY_EXISTING ~spwi924.spl~ ~override~
  SAY NAME1 @29241
  SAY UNIDENTIFIED_DESC @29242
IF_EXISTS BUT_ONLY

// ***** need to make them not exclusive in HLA tables
// ...or at least not bound by alignment

END

//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION tweak_spell_weap_apr BEGIN
// spell weapon APR
// find all items, set base APR to 2
COPY_EXISTING ~shillel.itm~ ~override~
				~shille.itm~ ~override~
				~shille2.itm~ ~override~
				~shille3.itm~ ~override~
				~fblade.itm~ ~override~
				~fblade1.itm~ ~override~
				~fblade2.itm~ ~override~
				~fblade3.itm~ ~override~
				~fblade4.itm~ ~override~
				~fblade5.itm~ ~override~
				~fblade6.itm~ ~override~
				~fblade7.itm~ ~override~
				~fblade8.itm~ ~override~
				~fblade9.itm~ ~override~
				~shamme1.itm~ ~override~
				~shamme2.itm~ ~override~
				~shamme3.itm~ ~override~
				~shammr.itm~ ~override~
				~shammr2.itm~ ~override~
				~shammr3.itm~ ~override~
				~shammr4.itm~ ~override~
				~shammr5.itm~ ~override~
				~b_iceb1.itm~ ~override~
				~b_iceb2.itm~ ~override~
				~b_iceb3.itm~ ~override~
				~b_iceb4.itm~ ~override~
				~b_iceb5.itm~ ~override~
				~b_iceb6.itm~ ~override~
				~b_iceb7.itm~ ~override~
				~b_iceb8.itm~ ~override~
				~b_iceb9.itm~ ~override~
				~b_iceb10.itm~ ~override~
				~moonbla.itm~ ~override~
				~cdimoonb.itm~ ~override~
				~smcudge.itm~ ~override~
				~cdismcud.itm~ ~override~
				~decasta.itm~ ~override~
				~cdideca.itm~ ~override~
				~phanblad.itm~ ~override~
				~blakblad.itm~ ~override~
				~msword.itm~ ~override~
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 2 parameter2 = 1 timing = 2 END
IF_EXISTS BUT_ONLY

ACTION_IF (FILE_CONTAINS_EVALUATED (~SPELL.IDS~ ~[ %TAB%]CLERIC_STONE_FIST[ %TAB%%LNL%%MNL%%WNL%]~)) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_STONE_FIST~ RET spell_res END
  OUTER_TEXT_SPRINT stone_fist_res ~%spell_res%~
  ACTION_IF (FILE_EXISTS_IN_GAME ~%stone_fist_res%.itm~) BEGIN
	COPY_EXISTING ~%stone_fist_res%.itm~ ~override~
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 2 parameter2 = 1 timing = 2 END
  END
END

END

//__________________________________________________________________________________
