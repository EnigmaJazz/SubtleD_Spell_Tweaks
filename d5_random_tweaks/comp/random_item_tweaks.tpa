
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								ITEM TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//DEFINE_ACTION_FUNCTION personal_item_tweaks BEGIN


// ring of danger sense_____________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_ring_danger_sense BEGIN
// AC, backstab immunity, spell evasion
COPY_EXISTING ~ring36.itm~ ~override~
  SAY IDENTIFIED_DESC @30102
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 292 target = 1 parameter2 = 1 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 1 parameter2 = 0 timing = 2 END
// 
END
//__________________________________________________________________________________


// gloves of missile snaring________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_gloves_missile_snaring BEGIN
// block 1 missile per round

LAF d5_resolve_state STR_VAR new_state_id = ~D5_SNAREARROW~ RET new_state_ind END
OUTER_SET missile_snaring_state = %new_state_ind%
OUTER_SET nada = RESOLVE_STR_REF (~ ~)
ACTION_IF NOT FILE_EXISTS_IN_GAME ~7eyes.2da~ BEGIN
	COPY ~might_and_guile/misc/7eyes.2da~ ~override~
END
APPEND ~7eyes.2da~ ~SNARE_ARROW  %missile_snaring_state%         %nada%         12*0x800000 *           *           *            *            *          *          *          * ~ UNLESS ~SNARE_ARROW~
COPY_EXISTING ~7eyes.2da~ ~override~
  COUNT_2DA_ROWS 1 rows
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 0 1 tag
	PATCH_IF (~%tag%~ STRING_EQUAL_CASE ~SNARE_ARROW~) BEGIN
	  SET 7eyes_row = row
	END
  END
BUT_ONLY

CREATE EFF ~d5misgl~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x18 1
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
  WRITE_LONG 0x24 0
  WRITE_LONG 0x28 600
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5misgl1~ #8
  WRITE_LONG 0x28 102

COPY ~d5_random_tweaks/misc/d5_base.spl~ ~override/d5misgl0.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter2 = %missile_snaring_state% timing = 1 END

COPY ~d5_random_tweaks/misc/d5_base.spl~ ~override/d5misgl1.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5misgl1~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 335 target = 1 power = 0 parameter1 = %missile_snaring_state% parameter2 = %7eyes_row% timing = 0 duration = 7 END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5misgl2~ END

COPY ~d5_random_tweaks/misc/d5_base.spl~ ~override/d5misgl2.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5misgl0~ END

COPY_EXISTING ~brac18.itm~ ~override~
  SAY IDENTIFIED_DESC @30202
  LPF DELETE_EFFECT INT_VAR match_opcode = 0 match_parameter2 = 2 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 83 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 305 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spra303~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spwi311~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~d5misgl0~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5misgl2~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 2 timing = 2 STR_VAR resource = ~d5misgl~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 3 parameter2 = 2 timing = 2 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________
