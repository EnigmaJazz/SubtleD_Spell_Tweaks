
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								ITEM TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//DEFINE_ACTION_FUNCTION personal_item_tweaks BEGIN


// ring of danger sense_____________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_ring_danger_sense BEGIN
// AC, backstab immunity, spell evasion...?
COPY_EXISTING ~ring36.itm~ ~override~
  SAY IDENTIFIED_DESC @30102
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 292 target = 1 parameter2 = 1 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 1 parameter2 = 0 timing = 2 END
IF_EXISTS BUT_ONLY
// 
END
//__________________________________________________________________________________


// gloves of missile snaring________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_gloves_missile_snaring BEGIN
// block 1 missile per round

LAF d5_resolve_state STR_VAR new_state_id = ~D5_SNAREARROW~ RET new_state_ind END
OUTER_SET missile_snaring_state = %new_state_ind%
OUTER_SET nada = RESOLVE_STR_REF (~ ~)
ACTION_IF NOT FILE_EXISTS_IN_GAME ~7eyes.2da~ BEGIN
	COPY ~d5_random_tweaks/data/item_tweaks/7eyes.2da~ ~override~
END
APPEND ~7eyes.2da~ ~SNARE_ARROW  %missile_snaring_state%         %nada%         12*0x800000 *           *           *            *            *          *          *          * ~ UNLESS ~SNARE_ARROW~
COPY_EXISTING ~7eyes.2da~ ~override~
  COUNT_2DA_ROWS 1 rows
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 0 1 tag
	PATCH_IF (~%tag%~ STRING_EQUAL_CASE ~SNARE_ARROW~) BEGIN
	  SET 7eyes_row = row
	END
  END
BUT_ONLY

CREATE EFF ~d5misgl~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x18 1
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
  WRITE_LONG 0x24 0
  WRITE_LONG 0x28 600
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5misgl1~ #8
  WRITE_LONG 0x28 102

COPY ~d5_random_tweaks/misc/d5_base.spl~ ~override/d5misgl1.spl~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5misgl1~ END
  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 335 target = 1 power = 0 parameter1 = %missile_snaring_state% parameter2 = %7eyes_row% timing = 0 duration = 7 END

COPY_EXISTING ~brac18.itm~ ~override~
  SAY IDENTIFIED_DESC @30202
  LPF DELETE_EFFECT INT_VAR match_opcode = 0 match_parameter2 = 2 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 83 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 305 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spra303~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spwi311~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 328 target = 1 parameter2 = %missile_snaring_state% timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 2 timing = 2 STR_VAR resource = ~d5misgl~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 3 parameter2 = 2 timing = 2 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// weapons that explode on hit_______________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_detonating_weapons BEGIN

ADD_PROJECTILE ~d5_random_tweaks/misc/b_pfire.pro~ 
COPY ~d5_random_tweaks/misc/PFIREA.bam~ ~override~
COPY ~d5_random_tweaks/misc/PFIREX.bam~ ~override~
COPY ~d5_random_tweaks/misc/#PRFIRE.vvc~ ~override~
COPY ~d5_random_tweaks/misc/#EFF_P45.WAV~ ~override~
COPY ~d5_random_tweaks/misc/#ARE_P03.WAV~ ~override~

COPY ~d5_random_tweaks/misc/d5_base.spl~ ~override/d5detwp.spl~
  LPF ALTER_SPELL_HEADER INT_VAR projectile = %b_pfire% END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 318 target = 2 parameter2 = 43 timing = 0 duration = 0 STR_VAR resource = ~%SOURCE_RES%~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 12 target = 2 power = 4 parameter1 = 0 parameter2 = 8 timing = 1 dicenumber = 5 dicesize = 6 savingthrow = (1 << 24) END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 power = 4 parameter1 = (0 - 1) timing = 0 duration = 6 STR_VAR resource = ~d5detwp~ END

COPY_EXISTING ~blun26.itm~ ~override~
			  ~blun26.itm~ ~override~
			  ~bdstaf03.itm~ ~override~
	LPF ALTER_EFFECT INT_VAR header_type = 1 match_opcode = 146 STR_VAR resource = ~d5detwp~ END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// skin of the forest_____________________________________________________________
//
DEFINE_ACTION_FUNCTION forest_leather_armor BEGIN
// recurring stoneskin, like carapace
// use alternate stonekin opcode, test against ironskin

CREATE EFF ~d5leat14~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x18 0
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
  WRITE_LONG 0x24 2
  WRITE_LONG 0x28 0
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5leat14~ #8
  WRITE_LONG 0x48 102

COPY_EXISTING ~leat14.itm~ ~override~
  SAY IDENTIFIED_DESC @30402
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~sppr202~ END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 0 match_parameter1 = 1 parameter1 = 5 END
// add skin effect
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5leat14~ END
IF_EXISTS BUT_ONLY

COPY ~d5_random_tweaks/data/item_tweaks/d5leat14.spl~ ~override~
//  LPF ALTER_EFFECT INT_VAR match_opcode = 321 STR_VAR resource = ~d5leat14~ END

COPY_EXISTING ~tomegol4.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
	REPLACE_TEXTUALLY ~!CheckStatGT(LastSummonerOf(Myself),0,STONESKINSGOLEM)~ ~~
	REPLACE_TEXTUALLY ~!CheckStatGT(Player[1-6],0,STONESKINSGOLEM)~ ~~
  COMPILE_BAF_TO_BCS
IF_EXISTS BUT_ONLY
END
//__________________________________________________________________________________


// IR rod of absorption_____________________________________________________________
//
DEFINE_ACTION_FUNCTION instant_rod_absorb BEGIN

COPY_EXISTING ~rods01.itm~ ~override~
	LPF ALTER_SPELL_HEADER INT_VAR target = 7 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Gargoyle Cloak___________________________________________________________________
//
DEFINE_ACTION_FUNCTION gargoyle_cloak BEGIN

ACTION_IF !(FILE_EXISTS_IN_GAME ~boot12.spl~) BEGIN
  COPY ~d5_random_tweaks/data/item_tweaks/boot12.spl~ ~override~
END

COPY_EXISTING ~bdclck05.itm~ ~override~
  SAY IDENTIFIED_DESC @30602
  LPF ALTER_ITEM_HEADER INT_VAR headcer_type = 3 charges = 2 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR resource = ~boot12~ END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Dwarven Thrower__________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_dwarven_thrower BEGIN

COPY_EXISTING ~hamm06.itm~ ~override~
			  ~hamm06a.itm~ ~override~
			  ~hamm06b.itm~ ~override~
	WRITE_BYTE 0x20 (THIS BAND 0b01111111)
	WRITE_BYTE 0x21 (THIS BAND 0b01100000)
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Methild's Harp___________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_instruments BEGIN

COPY_EXISTING ~misc3n.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~sppr201~ END
    FOR (index = 0x54 ; index >= 0x50 ; index -= 4) BEGIN
	READ_LONG "%index%" "valid"
	PATCH_IF ("%valid%" < 2147483646) AND ("%valid%" >= 0) BEGIN
	  READ_STRREF "%index%" "description"
	  SPRINT rf @30801
	  SPRINT rfa @30802
	  PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~%rf%~) = 0 BEGIN
		INNER_PATCH_SAVE new_desc ~%description%~ BEGIN
		  REPLACE_TEXTUALLY ~%rf%~ ~%rfa%~
		END
		SAY_EVALUATED "%index%" ~%new_desc%~
	  END
	END
  END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~misc3o.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~sppr307~ END
    FOR (index = 0x54 ; index >= 0x50 ; index -= 4) BEGIN
	READ_LONG "%index%" "valid"
	PATCH_IF ("%valid%" < 2147483646) AND ("%valid%" >= 0) BEGIN
	  READ_STRREF "%index%" "description"
	  SPRINT rp @30811
	  SPRINT rprc @30812
	  SPRINT rpbe @30813
	  PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~%rp%~) = 0 BEGIN
		INNER_PATCH_SAVE new_desc ~%description%~ BEGIN
		  PATCH_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
			REPLACE_TEXTUALLY ~%rp%~ ~%rprc%~
		  END
		  PATCH_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
			REPLACE_TEXTUALLY ~%rp%~ ~%rpbe%~
		  END
		END
		SAY_EVALUATED "%index%" ~%new_desc%~
	  END
	END
  END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Edwin's Amulet___________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_edwin_amulet INT_VAR slot_reduction = 0 level_reduction = 0 BEGIN

COPY_EXISTING ~misc89.itm~ ~override~
  PATCH_IF slot_reduction = 1 BEGIN
	SAY UNIDENTIFIED_DESC @30912
	LPF ALTER_EFFECT INT_VAR match_opcode = 42 match_parameter1 = 2 parameter1 = 1 END
  END
  PATCH_IF level_reduction = 1 BEGIN
	SAY UNIDENTIFIED_DESC @30922
	LPF DELETE_EFFECT INT_VAR match_opcode = 42 match_parameter2 = 128 END
	LPF DELETE_EFFECT INT_VAR match_opcode = 42 match_parameter2 = 256 END
  END  
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


